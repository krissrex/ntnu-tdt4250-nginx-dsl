grammar no.ntnu.tdt4250.Nginx with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate nginx "http://www.ntnu.no/tdt4250/Nginx"

Nginx: 
	includes+=Include* & 
	sites+=Site*;
	
	
SiteName returns ecore::EString: ID ('.' ID)*;
WildCardedSiteName returns ecore::EString: ('*.')? ID ('.' ID)* ('.*')?; // http://nginx.org/en/docs/http/server_names.html#wildcard_names

Include:
    'INCLUDE' importURI=STRING
;

// TODO add an include?
// TODO add reference in itself
// TODO maybe add location directive? 
Site: 
	name=(SiteName|'default')
	alternativeNames+=WildCardedSiteName* 
	('from' inherits=[Site | SiteName])?
	':'
	BEGIN
		(
			('index:' index=STRING)? &
			('error_page:' errorPage+=ErrorPage)* &
			(sslCert=SslCert)? &
			('log_name:' logName=STRING)? &
			(
				('port:' port=INT) |
				('root:' root=FilePath)
			) &
			('template:' template=('php7.2' | 'php5.6'))? &
			('include:' includes+=STRING)* &
			('https_redirect:' httpsRedirect=OptionalBoolean)? &
			('gzip:' gzip=OptionalBoolean)? 
		)
	END
;

SslCert:
	'ssl_certificate:' sslCert=FilePath &
	'ssl_certificate_key:' sslCertKey=FilePath &
	'ssl_dhparam:' dhParam=FilePath
;

ErrorPage: httpCodes+=INT+ uri=FilePath;

FilePath returns ecore::EString: STRING;
OptionalBoolean returns ecore::EBooleanObject: 'true'|'false';

// Synthetic allows for white-space sensitive languages like python and yaml
// https://www.eclipse.org/Xtext/documentation/307_special_languages.html
terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';