grammar no.ntnu.tdt4250.Nginx with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate nginx "http://www.ntnu.no/tdt4250/Nginx"

Nginx: 
	sites+=Site*;
	
SiteName: ID ('.' ID)*;
WildCardedSiteName: (ID | '*') ('.' (ID | '*'))*;

// TODO fix port and root??
// TODO add an include?
// TODO add reference in itself
// TODO maybe add location directive? 
Site: 
	name=(SiteName|'default')
	alternativeNames+=WildCardedSiteName* ':'
	BEGIN
		(
			('index:' index=STRING)? &
			('error_page:' errorPage+=ErrorPage)* &
			(sslCert=SslCert)? &
			('log_name:' logName=STRING)? &
			(
				('port:' port=INT) |
				('root:' root=FilePath)
			) &
			('template:' template=Template)? &
			('include:' includes+=STRING)* &
			('https_redirect:' httpsRedirect=OptionalBoolean)? &
			('gzip:' gzip=OptionalBoolean)? 
		)
	END
;
// TODO handle php? als in .mtl 
Template: 'php7.2' | 'php5.6';

SslCert:
	'ssl_certificate:' sslCert=FilePath &
	'ssl_certificate_key:' sslCertKey=FilePath &
	'ssl_dhparam:' dhParam=FilePath
;

ErrorPage: httpCodes+=INT+ uri=FilePath;

FilePath returns ecore::EString: STRING;
OptionalBoolean returns ecore::EBooleanObject: 'true'|'false'|'TRUE'|'FALSE'|'yes'|'no'|'YES'|'NO';

// Synthetic allows for white-space sensitive languages like python and yaml
// https://www.eclipse.org/Xtext/documentation/307_special_languages.html
terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';